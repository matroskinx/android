apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def keystorePropertiesFile = file('keystore.properties')
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def verName = grgit.describe(tags: true, longDescr: true)

def dot_patch = verName.indexOf('.', verName.indexOf('.') + 1)

def majorMinor = verName.substring(0, dot_patch)

def dot_minor = verName.indexOf('.')

def minor = verName.substring(dot_minor + 1, dot_patch).toInteger()

def left_border = verName.indexOf("-")
def right_border = verName.indexOf("-", left_border + 1)
def patch = verName.substring(left_border + 1, right_border)
def verCode = grgit.tag.list().size + minor * 10 + patch.toInteger()

android {
    signingConfigs {
        sign_release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28


    defaultConfig {
        applicationId "com.example.vladislav.android5"
        minSdkVersion 23
        targetSdkVersion 23
        versionCode verCode
        versionName majorMinor + '.' + patch
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sign_release
        }

        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "Android5-${variant.versionName}.apk"
            }

        }
    }
    flavorDimensions "version"
    productFlavors {
        developer {
            versionNameSuffix "-dev"
        }
        master {

        }
    }

    sourceSets { debug { java.srcDirs = ['src/debug/java'] } }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0-rc02'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:design:28.0.0-rc02'
}
